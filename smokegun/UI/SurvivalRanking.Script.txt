#Include "Libs/smokegun/Structs.Script.txt" as Structs

#Struct Structs::SSurvivalPlayer as SSurvivalPlayer

declare Boolean G_IsEnabled;

Text GetLayer() {
    return """
<manialink version="3" name="Obstacle:SurvivalRanking">
<frame id="SurvivalLives" pos="139 7">
    <frame id="Frame_Rankings" pos="-20 -8" size="41 37" scroll="1" scrollmax="0 170">
        <quad size="41 200" scriptevents="1" />
        <frameinstance modelid="survival_ranking" pos="0 0" id="ranking0" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -4" id="ranking1" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -8" id="ranking2" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -12" id="ranking3" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -16" id="ranking4" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -20" id="ranking5" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -24" id="ranking6" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -28" id="ranking7" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -32" id="ranking8" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -36" id="ranking9" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -40" id="ranking10" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -44" id="ranking11" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -48" id="ranking12" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -52" id="ranking13" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -56" id="ranking14" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -60" id="ranking15" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -64" id="ranking16" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -68" id="ranking17" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -72" id="ranking18" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -76" id="ranking19" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -80" id="ranking20" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -84" id="ranking21" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -88" id="ranking22" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -92" id="ranking23" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -96" id="ranking24" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -100" id="ranking25" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -104" id="ranking26" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -108" id="ranking27" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -112" id="ranking28" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -116" id="ranking29" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -120" id="ranking30" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -124" id="ranking31" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -128" id="ranking32" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -132" id="ranking33" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -136" id="ranking34" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -140" id="ranking35" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -144" id="ranking36" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -148" id="ranking37" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -152" id="ranking38" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -156" id="ranking39" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -160" id="ranking40" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -164" id="ranking41" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -168" id="ranking42" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -172" id="ranking43" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -176" id="ranking44" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -180" id="ranking45" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -184" id="ranking46" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -188" id="ranking47" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -192" id="ranking48" hidden="0"/>
        <frameinstance modelid="survival_ranking" pos="0 -196" id="ranking49" hidden="0"/>
    </frame>
    <frame id="Frame_Background"halign="center" valign="center2">
        <label pos="-18.5 -3.5" z-index="0" size="5 5" text="ï¢" id="Label_Survival_Top" scriptevents="1" halign="center" valign="center2"/>
        <label pos="-13.5 -3.5" z-index="0" size="5 5" text="ðŸ”"id="Label_Survival_Search" scriptevents="1" halign="center" valign="center2"/>
        <label pos="0 -1" size="40 5" text="Survival" style="TextTitle3" textsize="4" halign="center" textfont="OswaldMono" maxline="1" valign="top" textemboss="1"/>
        <quad z-index="-1" size="44 47" style="BgRaceScore2" substyle="BgCardPlayer" halign="center" valign="top"/>
    </frame>
    <framemodel id="survival_ranking">
        <label id="rank"  pos="0 0" size="6 4" halign="left" valign="top" style="TextCardSmallScores2" textsize="1" text="1." textfont="OswaldMono"/>
        <label id="name"  pos="6 0" size="20 4" halign="left" valign="top" textsize="1" text="Mon Super Pseudo" textfont="Oswald"/>
        <label id="lives" pos="40 0" size="14 4" halign="right" valign="top" style="TextTitle2" textsize="1" text="100" textfont="OswaldMono"/>
    </framemodel>
</frame>
<script><!--

{{{Structs::GetSSurvivalPlayerStruct()}}}

declare CMlFrame Frame_Rankings;
declare CMlFrame Frame_SurvivalLives;
declare Integer LocalPlayerIndex;
declare CSmPlayer LocalPlayer;

Void UpdateView()
{
    declare netread SSurvivalPlayer[] Net_SurvivalPlayers for Teams[0] = [];

    declare Integer[Integer] LivesByIdx = []; // PlayerIdx => Lives

    foreach (Idx => SurvivalPlayer in Net_SurvivalPlayers) {
        LivesByIdx[Idx] = SurvivalPlayer.Lives;
    }

    LivesByIdx = LivesByIdx.sortreverse();
    declare Integer[] SortedIdx = [];
    foreach (Idx => Lives in LivesByIdx) {
        SortedIdx.add(Idx);
    }

    declare NewLocalPlayerIndex = -1;

    declare LastLives = -1;
    declare Rank = 0;

    foreach (Idx => Control in Frame_Rankings.Controls) {
        if (Idx == 0) continue;

        declare Frame <=> (Control as CMlFrame);
        declare RankLabel <=> (Frame.GetFirstChild("rank") as CMlLabel);
        declare NameLabel <=> (Frame.GetFirstChild("name") as CMlLabel);
        declare LiveLabel <=> (Frame.GetFirstChild("lives") as CMlLabel);

        if (Idx-1 < SortedIdx.count) {
            declare SurvivalPlayer  = Net_SurvivalPlayers[SortedIdx[Idx-1]];
            if (SurvivalPlayer.Lives != LastLives) {
                LastLives = SurvivalPlayer.Lives;
                Rank += 1;
            }

            RankLabel.SetText(""^Rank);
            NameLabel.SetText(SurvivalPlayer.Nickname);
            LiveLabel.SetText(""^SurvivalPlayer.Lives^" -"^SurvivalPlayer.WillLoose);

            if (LocalPlayer != Null && SurvivalPlayer.Login == LocalPlayer.User.Login) {
                NewLocalPlayerIndex = Idx;
            }

            Control.Show();
        }
        else {
            Control.Hide();
        }
    }
    LocalPlayerIndex = NewLocalPlayerIndex;
}

main()
{
    Frame_Rankings = (Page.GetFirstChild("Frame_Rankings") as CMlFrame);
    Frame_SurvivalLives = (Page.GetFirstChild("SurvivalLives") as CMlFrame);
    LocalPlayerIndex = -1;

    declare netread Net_LayerSurvivalRankingEnabled for Teams[0] = False;
    declare netread Net_LayerSurvivalRankingUpdated for Teams[0] = 0;
    declare LastUpdated = -1;

    while (True) {
        yield;

        if (Net_LayerSurvivalRankingEnabled) {
            if (!Frame_SurvivalLives.Visible) {
                Frame_SurvivalLives.Show();
            }
        }
        else {
            if (Frame_SurvivalLives.Visible) {
                Frame_SurvivalLives.Hide();
            }
        }

        if (!Net_LayerSurvivalRankingEnabled) continue;

        LocalPlayer <=> InputPlayer;
        if (GUIPlayer != Null) LocalPlayer <=> GUIPlayer;

        if (Net_LayerSurvivalRankingUpdated != LastUpdated) {
           UpdateView();
           LastUpdated = Net_LayerSurvivalRankingUpdated;
        }

        foreach (Event in PendingEvents) {
            switch(Event.Type) {
                case CMlScriptEvent::Type::MouseClick: {
                    switch(Event.Control.ControlId) {
                        case "Label_Survival_Top": {
                            // scroll to top of the list
                            Frame_Rankings.ScrollOffset = <0., 0.>;
                        }
                        case "Label_Survival_Search": {
                            if (LocalPlayerIndex == -1) {
                                // scroll to top of the list
                                Frame_Rankings.ScrollOffset = <0., 0.>;
                            }
                            else {
                                // scroll to the player index in the list
                                Frame_Rankings.ScrollOffset = <0., (LocalPlayerIndex-1) * 4.>;
                            }
                        }
                    }
                }
            }
        }
    }
}

--></script>
</manialink>
    """;
}

Void UpdateLayer()
{
    if (G_IsEnabled)
    {
        declare SSurvivalPlayer[] G_SurvivalPlayers for Teams[0] = [];
        declare netwrite SSurvivalPlayer[] Net_SurvivalPlayers for Teams[0] = [];
        declare netwrite Net_LayerSurvivalRankingUpdated for Teams[0] = 0;
        Net_LayerSurvivalRankingUpdated = Now;
        Net_SurvivalPlayers = G_SurvivalPlayers;
    }
}

Void OnSurvivalStart()
{
    G_IsEnabled = True;
    declare netwrite Net_LayerSurvivalRankingEnabled for Teams[0] = False;
    Net_LayerSurvivalRankingEnabled = True;
}

Void OnSurvivalEnd()
{
    G_IsEnabled = False;
    declare netwrite Net_LayerSurvivalRankingEnabled for Teams[0] = False;
    Net_LayerSurvivalRankingEnabled = False;
}
